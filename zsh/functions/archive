# vim:et sts=2 sw=2 ft=zsh
#
# Creates archive files
#

if (( # < 2 )); then
  print -u2 "usage: ${0} <archive_name.ext> <file>..."
  return 1
fi

# we are quitting (above) if there are less than 2 vars,
# so we don't need any argc check here.

# pigz and pbzip2 are aliased in the init.zsh file. This provides a significant speedup, resulting in a
# near-liner decrease in compression time based on on the number of available cores.

case "${1}" in
  (*.7z) 7za a "${@}" ;;
  (*.rar) rar a "${@}" ;;
  (*.tar.bz|*.tar.bz2|*.tbz|*.tbz2) tar -cvjf "${@}" ;;
  (*.tar.gz|*.tgz) tar -cvzf "${@}" ;;
  (*.tar.lzma|*.tlz) tar --lzma --help &>/dev/null && env XZ_OPT=-T0 tar --lzma -cvf "${@}" ;;
  (*.tar.xz|*.txz) tar -J --help &>/dev/null && env XZ_OPT=-T0 tar -cvJf "${@}" ;;
  (*.tar) tar -cvf "${@}" ;;
  (*.zip) zip -r "${@}" ;;
  (*.zst) zstd -c -T0 "${@:2}" -o "${1}" ;;
  (*.bz|*.bz2) print "${0}: .bzip2 is only useful for single files, and does not capture permissions. Use .tar.bz2" ;;
  (*.gz) print "${0}: .gz is only useful for single files, and does not capture permissions. Use .tar.gz" ;;
  (*.lzma) print "${0}: .lzma is only useful for single files, and does not capture permissions. Use .tar.lzma" ;;
  (*.xz) print "${0}: .xz is only useful for single files, and does not capture permissions. Use .tar.xz" ;;
  (*.Z) print "${0}: .Z is only useful for single files, and does not capture permissions." ;;
  (*) print "${0}: unknown archive type: ${1}" ;;
esac
