#!/bin/zsh

unlimit
umask 022

typeset -U path

path=(
  $CARGO_HOME/bin
  $GOPATH/bin
  $HOME/.local/bin
  /usr/local/bin
  /usr/bin
  /bin
  $path
)

fpath=(
  $ZDOTDIR/functions
  $ZDOTDIR/functions/custom
  $fpath
)

export VISUAL=nvim
export EDITOR=$VISUAL
export GIT_EDITOR=$VISUAL

export FZFZ_SUBDIR_LIMIT=0 # dont add directories to fzfz
export FZF_COMPLETION_OPTS="--preview '(bat --color=always {} || cat {} || tree -C {}) 2> /dev/null | head -200'"
export FZF_DEFAULT_COMMAND='rg --files --hidden --glob "!.git" --glob "!node_modules/*"'
export FZF_DEFAULT_OPTS="--ansi --select-1 --exit-0 --height 40% --reverse --cycle"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_CTRL_T_OPTS="$FZF_COMPLETION_OPTS"
export FZF_ALT_C_COMMAND='fd --type d --hidden --follow --exclude .git'
export FZF_ALT_C_OPTS="--preview 'tree -C {} | head -100'"

export GPG_TTY=$TTY
export HISTFILE="$XDG_DATA_HOME/zsh/history"
export HISTSIZE=1000000
export HOST=${HOST:-$HOSTNAME}
export LESS="-R -M --shift 5"
export REPORTTIME=5
export SAVEHIST=$HISTSIZE
export SPROMPT='uh, you meant "%r", right? [yn] '
export WATCH=notme
export WATCHFMT="[%B%t%b] %B%n%b has %a %B%l%b from %B%M%b"

disable r # disable r zsh builtin

setopt \
  auto_name_dirs       \
  braceccl             \
  chase_links          \
  clobber              \
  complete_aliases     \
  extended_glob        \
  hist_find_no_dups    \
  hist_save_no_dups    \
  hist_reduce_blanks   \
  list_packed          \
  list_types           \
  mark_dirs            \
  no_beep              \
  no_hup               \
  no_flow_control      \
  notify               \
  path_dirs            \
  rec_exact            \
  rm_star_silent

autoload -U $ZDOTDIR/functions/*(:t)
autoload -U $ZDOTDIR/functions/custom/*(:t)
autoload -U backward-kill-word-match && zle -N backward-kill-word-match
autoload -U tetriscurses
autoload -U url-quote-magic && zle -N self-insert url-quote-magic
autoload -U zargs
autoload -U zcalc
autoload -U zmv
autoload -U zsh-mime-setup && zsh-mime-setup

compdef b=bat &> /dev/null
compdef f=fd &> /dev/null
compdef d=docker &> /dev/null
compdef g=git &> /dev/null
compdef k=kubectl &> /dev/null
compdef s=ssh &> /dev/null
compdef r=rg &> /dev/null
compdef rr=rg &> /dev/null
compdef v=nvim &> /dev/null
compdef y=yarn &> /dev/null

bindkey -e
bindkey '^Q' push-line
bindkey '^W' backward-kill-word-match
bindkey -s '^F' '^Upushd >/dev/null; dirs -v^M'
bindkey -s '^B' '^Upopd >/dev/null; dirs -v^M'
bindkey -M menuselect '^M' .accept-line # enter on the menu accepts the line

zstyle ':zle:*' word-style normal
zstyle ':zle:*' word-chars ${WORDCHARS//\//}

rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

_zsh_cli_fg() { fg; }
zle -N _zsh_cli_fg
bindkey '^Z' _zsh_cli_fg
