#!/bin/zsh
# zsh config

# {{{1 environment

unlimit
umask 022

export EDITOR=vim
export HISTFILE=~/.zsh/history
export HISTSIZE=1000000
export HOST=${HOST:-$HOSTNAME}
export REPORTTIME=5
export SAVEHIST=$HISTSIZE
export SPROMPT='uh, you meant "%r", right? [yn] '
export LESS="-R -M --shift 5"
export WATCH=notme
export WATCHFMT="[%B%t%b] %B%n%b has %a %B%l%b from %B%M%b"

# {{{1 paths

path=($HOME/local/bin /bin /usr/local/bin /usr/bin $path)

# {{{1 options

setopt \
    all_export           \
    auto_name_dirs       \
    braceccl             \
    chase_links          \
    clobber              \
    complete_aliases     \
    extended_glob        \
    hist_find_no_dups    \
    hist_save_no_dups    \
    hist_reduce_blanks   \
    list_packed          \
    list_types           \
    mark_dirs            \
    no_beep              \
    no_hup               \
    no_flow_control      \
    notify               \
    path_dirs            \
    rec_exact            \
    rm_star_silent       

# {{{1 autoload

autoload -U url-quote-magic && zle -N self-insert url-quote-magic
autoload -U zargs
autoload -U zcalc
autoload -U zmv
autoload -U zsh-mime-setup && zsh-mime-setup

zcalc () { print $(( ans = ${@:-ans} )) }
alias calc='noglob zcalc'

# {{{1 keys

bindkey -e
bindkey '^Q' push-line
# bindkey '^R' history-incremental-search-backward # replaced by zdharma/history-search-multi-word
# bindkey '^S' history-incremental-search-forward
bindkey -s '^F' '^Upushd >/dev/null; dirs -v^M'
bindkey -s '^B' '^Upopd >/dev/null; dirs -v^M'

bindkey -M menuselect '^M' .accept-line # enter on the menu accepts the line

autoload -U backward-kill-word-match
zle -N backward-kill-word-match
bindkey '^W' backward-kill-word-match

zstyle ':zle:*' word-style normal
zstyle ':zle:*' word-chars ${WORDCHARS//\//}

rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

# git clone suffix
git-clone-suffix() { git clone $1 ${$(basename $1)%.git} }
alias -s 'git'='git-clone-suffix'
