#!/bin/zsh

unlimit
umask 022

path=(
  $HOME/.local/bin
  $HOME/local/bin
  /bin
  /usr/local/bin
  /usr/bin
  $path
)

export EDITOR=nvim
export FZFZ_SUBDIR_LIMIT=0 # dont add directories to fzfz
export FZF_DEFAULT_COMMAND='rg --files --hidden --glob "!.git" --glob "!node_modules/*"'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_DEFAULT_OPTS="--ansi --select-1 --exit-0 --height 40% --reverse --cycle"
export FZF_COMPLETION_OPTS="--preview '(bat --color=always {} || cat {} || tree -C {}) 2> /dev/null | head -200'"
export FZF_CTRL_T_OPTS="$FZF_COMPLETION_OPTS"
export GIT_EDITOR=nvim
export HISTFILE=~/.zsh/history
export HISTSIZE=1000000
export HOST=${HOST:-$HOSTNAME}
export LESS="-R -M --shift 5"
export REPORTTIME=5
export SAVEHIST=$HISTSIZE
export SPROMPT='uh, you meant "%r", right? [yn] '
export WATCH=notme
export WATCHFMT="[%B%t%b] %B%n%b has %a %B%l%b from %B%M%b"

setopt \
  auto_name_dirs       \
  braceccl             \
  chase_links          \
  clobber              \
  complete_aliases     \
  extended_glob        \
  hist_find_no_dups    \
  hist_save_no_dups    \
  hist_reduce_blanks   \
  list_packed          \
  list_types           \
  mark_dirs            \
  no_beep              \
  no_hup               \
  no_flow_control      \
  notify               \
  path_dirs            \
  rec_exact            \
  rm_star_silent

autoload -U url-quote-magic && zle -N self-insert url-quote-magic
autoload -U zargs
autoload -U zcalc
autoload -U zmv
autoload -U zsh-mime-setup && zsh-mime-setup

zcalc () { print $(( ans = ${@:-ans} )) }
alias calc='noglob zcalc'

bindkey -e
bindkey '^Q' push-line
bindkey -s '^F' '^Upushd >/dev/null; dirs -v^M'
bindkey -s '^B' '^Upopd >/dev/null; dirs -v^M'

bindkey -M menuselect '^M' .accept-line # enter on the menu accepts the line

autoload -U backward-kill-word-match
zle -N backward-kill-word-match
bindkey '^W' backward-kill-word-match

zstyle ':zle:*' word-style normal
zstyle ':zle:*' word-chars ${WORDCHARS//\//}

rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

# git clone suffix
git-clone-suffix() { git clone $1 ${$(basename $1)%.git} }
alias -s 'git'='git-clone-suffix'

# No arguments: `git status`
# With arguments: acts like `git`
unalias g # unalias zsh git plugin
g() {
  if [[ $# -gt 0 ]]; then
    git "$@"
  else
    git status
  fi
}
